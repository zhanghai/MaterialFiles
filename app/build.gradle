/*
 * Copyright (c) 2018 Hai Zhang <dreaming.in.code.zh@gmail.com>
 * All Rights Reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

apply from: '../signing.gradle'

//#ifdef NONFREE
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'
    }
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
//#endif

android {
    namespace 'me.zhanghai.android.files'
    compileSdkVersion 33
    ndkVersion '25.2.9519653'
    buildToolsVersion '33.0.2'
    defaultConfig {
        applicationId 'me.zhanghai.android.files'
        minSdkVersion 21
        // Not supporting notification runtime permission yet.
        //noinspection OldTargetApi
        targetSdkVersion 32
        versionCode 31
        versionName '1.5.2'
        resValue 'string', 'app_version', versionName + ' (' + versionCode + ')'
        buildConfigField 'String', 'FILE_PROVIDIER_AUTHORITY', 'APPLICATION_ID + ".file_provider"'
        resValue 'string', 'app_provider_authority', applicationId + '.app_provider'
        resValue 'string', 'file_provider_authority', applicationId + '.file_provider'
    }
    buildFeatures {
        aidl true
        buildConfig true
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
//#ifdef NONFREE
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
//#endif
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }
    lint {
        // For "Invalid package reference in library; not included in Android: javax.security.sasl.
        // Referenced from org.apache.mina.proxy.ProxyAuthException."
        warning 'InvalidPackage', 'MissingTranslation'
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}
dependencies {
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    releaseImplementation 'com.github.mypplication:stetho-noop:1.1'
    implementation 'com.github.topjohnwu.libsu:service:5.0.5'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlinx-coroutines-android depends on kotlin-stdlib-jdk8
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    def kotlinx_coroutines_version = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"

    implementation 'androidx.activity:activity-ktx:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    implementation 'androidx.fragment:fragment-ktx:1.5.7'
    def androidx_lifecycle_version = '2.6.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    debugImplementation 'com.facebook.stetho:stetho:1.6.0'
    // If added along with okhttp3, remove the related dontwarn from proguard-rules.pro.
    //debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.6.0'
    implementation 'com.h6ah4i.android.materialshadowninepatch:materialshadowninepatch:1.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation ('com.hierynomus:smbj:0.11.5') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation ('com.hierynomus:sshj:0.35.0') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.6'
    implementation 'com.leinardi.android:speed-dial:3.3.0'
    implementation ('com.rapid7.client:dcerpc:0.12.0') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    // SMBJ-RPC depends on com.google.guava:guava:18.0, which conflicts with
    // com.google.guava:listenablefuture:1.0 pulled in by AndroidX Core
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'commons-net:commons-net:3.9.0'
    // LicensesDialog 2.2.0 pulls in androidx.webkit and uses setForceDark() instead of correctly
    // setting colors.
    //noinspection GradleDependency
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    // dev.chrisbanesinsetter:insetter:0.6.0 makes inset unstable when entering immersive.
    implementation 'dev.chrisbanes:insetter-ktx:0.3.1'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    // Shizuku-API 13.1.0 requires core library desugaring.
    //noinspection GradleDependency
    implementation 'dev.rikka.shizuku:api:12.2.0'
    implementation ('eu.agno3.jcifs:jcifs-ng:2.1.9') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.73'
    def coil_version = '2.3.0'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.5.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.2.0'
    implementation 'me.zhanghai.android.foregroundcompat:library:1.0.2'
    implementation 'me.zhanghai.android.libselinux:library:2.1.0'
    implementation 'me.zhanghai.android.retrofile:library:1.1.1'
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.4'
    // Commons Compress 1.21 requires Android 8.0.
    // See also https://issues.apache.org/jira/browse/COMPRESS-611
    // Run `git revert fb1628e` if we ever created a fork that supports Android 5.0, or we raised
    // minimum SDK version to Android 8.0.
    //noinspection GradleDependency
    implementation 'org.apache.commons:commons-compress:1.20'
    // Optional dependency of Commons Compress for 7Z LZMA.
    implementation 'org.tukaani:xz:1.9'
    implementation 'org.apache.ftpserver:ftpserver-core:1.2.0'
    // This is a dependency of org.apache.ftpserver:ftpserver-core but org.apache.mina:mina-core
    // 2.1.3+ became incompatible before API 24 due to dependency on StandardSocketOptions
    // (DIRMINA-1123) and NetworkChannel.supportedOptions() (DIRMINA-1130).
    implementation ('org.apache.mina:mina-core') {
        version {
            strictly '2.1.3'
        }
    }
    // Also a dependency of jCIFS-NG and Junrar.
    implementation 'org.slf4j:slf4j-android:1.7.36'

//#ifdef NONFREE
    implementation 'com.github.junrar:junrar:7.5.4'
    implementation platform('com.google.firebase:firebase-bom:31.5.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
//#endif
}
