/*
 * Copyright (c) 2018 Hai Zhang <dreaming.in.code.zh@gmail.com>
 * All Rights Reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
kapt {
    correctErrorTypes = true
}
apply plugin: 'kotlin-parcelize'

apply from: '../signing.gradle'

//#ifdef NONFREE
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
//#endif

android {
    compileSdkVersion 31
    ndkVersion '22.1.7171670'
    // Build tools 31.0.0 broke librootjava: https://github.com/Chainfire/librootjava/issues/24 .
    //buildToolsVersion '31.0.0'
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'me.zhanghai.android.files'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 25
        versionName '1.3.1'
        resValue 'string', 'app_version', versionName + ' (' + versionCode + ')'
        buildConfigField 'String', 'FILE_PROVIDIER_AUTHORITY', 'APPLICATION_ID + ".file_provider"'
        resValue 'string', 'app_provider_authority', applicationId + '.app_provider'
        resValue 'string', 'file_provider_authority', applicationId + '.file_provider'
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                '-Xopt-in=kotlin.RequiresOptIn',
        ]
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
//#ifdef NONFREE
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
//#endif
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        // For "Invalid package reference in library; not included in Android: javax.security.sasl.
        // Referenced from org.apache.mina.proxy.ProxyAuthException."
        warning 'InvalidPackage', 'MissingTranslation'
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}
dependencies {
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    releaseImplementation 'com.github.mypplication:stetho-noop:1.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def kotlinx_coroutines_version = '1.5.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    def androidx_lifecycle_version = '2.3.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    debugImplementation 'com.facebook.stetho:stetho:1.6.0'
    // If added along with okhttp3, remove the related dontwarn from proguard-rules.pro.
    //debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.6.0'
    implementation 'com.github.junrar:junrar:7.4.0'
    implementation 'com.h6ah4i.android.materialshadowninepatch:materialshadowninepatch:1.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    // SMBJ 0.11.1 is incompatible with SMBJ-RPC.
    // https://github.com/zhanghai/MaterialFiles/issues/542
    // Run `git revert 5ec1a3f` once SMBJ-RPC is updated to be compatible with the new SMBJ.
    implementation ('com.hierynomus:smbj:0.10.0') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation ('com.hierynomus:sshj:0.31.0') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'
    // FloatingActionButtonSpeedDial 3.2.0 forces the user out of touch mode upon open and leaves a
    // strange focus effect on the first mini FAB.
    //noinspection GradleDependency
    implementation 'com.leinardi.android:speed-dial:3.1.1'
    implementation ('com.rapid7.client:dcerpc:0.10.0') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.68'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    // LicensesDialog 2.2.0 pulls in androidx.webkit and uses setForceDark() instead of correctly
    // setting colors.
    //noinspection GradleDependency
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    // dev.chrisbanesinsetter:insetter:0.6.0 makes inset unstable when entering immersive.
    implementation 'dev.chrisbanes:insetter-ktx:0.3.1'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.shizuku:api:11.0.3'
    implementation 'eu.chainfire:librootjava:1.3.1'
    implementation 'eu.chainfire:libsuperuser:1.1.0.201907261845'
    implementation 'eu.agno3.jcifs:jcifs-ng:2.1.6'
    def coil_version = '1.3.2'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
    implementation 'me.zhanghai.android.effortlesspermissions:library:1.1.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
    implementation 'me.zhanghai.android.foregroundcompat:library:1.0.1'
    implementation 'me.zhanghai.android.libselinux:library:2.0.1'
    implementation 'me.zhanghai.android.retrofile:library:1.1.1'
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.3'
    implementation 'org.apache.commons:commons-compress:1.20'
    // Optional dependency of Commons Compress for 7Z LZMA.
    implementation 'org.tukaani:xz:1.9'
    implementation 'org.apache.ftpserver:ftpserver-core:1.1.1'
    // Also a dependency of jCIFS-NG and Junrar.
    implementation 'org.slf4j:slf4j-android:1.7.31'

//#ifdef NONFREE
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:18.2.0'
//#endif
}
