/*
 * Copyright (c) 2018 Hai Zhang <dreaming.in.code.zh@gmail.com>
 * All Rights Reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

apply from: '../signing.gradle'

//#ifdef NONFREE
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
    }
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
//#endif

android {
    namespace 'me.zhanghai.android.files'
    buildToolsVersion = '35.0.0'
    compileSdk 35
    ndkVersion '27.1.12297006'
    defaultConfig {
        applicationId 'me.zhanghai.android.files'
        minSdk 21
        // Not supporting foreground service timeout yet.
        //noinspection OldTargetApi
        targetSdk 34
        versionCode 39
        versionName '1.7.4'
        resValue 'string', 'app_version', versionName + ' (' + versionCode + ')'
        buildConfigField 'String', 'FILE_PROVIDIER_AUTHORITY', 'APPLICATION_ID + ".file_provider"'
        resValue 'string', 'app_provider_authority', applicationId + '.app_provider'
        resValue 'string', 'file_provider_authority', applicationId + '.file_provider'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
        viewBinding true
    }
    androidResources {
        generateLocaleConfig true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    lint {
        // For "Invalid package reference in library; not included in Android: javax.security.sasl.
        // Referenced from org.apache.mina.proxy.ProxyAuthException."
        warning 'InvalidPackage', 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
//#ifdef NONFREE
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
//#endif
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES',
                    'org/bouncycastle/pqc/crypto/picnic/*'
            ]
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}
dependencies {
    implementation('com.github.bitfireAT:dav4jvm:c1bc143') {
        exclude group: 'org.ogce', module: 'xpp3'
    }
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.topjohnwu.libsu:service:5.2.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.2")

    // kotlinx-coroutines-android depends on kotlin-stdlib-jdk8
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    def kotlinx_coroutines_version = '1.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"

    implementation 'androidx.activity:activity-ktx:1.9.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.fragment:fragment-ktx:1.8.4'
    def androidx_lifecycle_version = '2.8.6'
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'com.h6ah4i.android.materialshadowninepatch:materialshadowninepatch:1.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    // SMBJ 0.12.0 breaks anonymous authentication: https://github.com/hierynomus/smbj/issues/792
    //noinspection GradleDependency
    implementation ('com.hierynomus:smbj:0.11.5') {
        // org.bouncycastle:bcprov-jdk15on uses bytecode version unsupported by Jetifier, so use
        // org.bouncycastle:bcprov-jdk15to18 instead.
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    def bouncy_castle_version = '1.78.1'
    implementation "org.bouncycastle:bcprov-jdk15to18:$bouncy_castle_version"
    implementation ('com.hierynomus:sshj:0.39.0') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
    }
    implementation "org.bouncycastle:bcpkix-jdk15to18:$bouncy_castle_version"
    implementation 'com.leinardi.android:speed-dial:3.3.0'
    implementation ('com.rapid7.client:dcerpc:0.12.1') {
        // SMBJ-RPC depends on the JRE flavor of Guava which targets Java 8.
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'com.google.guava:guava:33.3.0-android'
    // Guava conflicts with com.google.guava:listenablefuture:1.0 pulled in by AndroidX Core
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'commons-net:commons-net:3.11.1'
    // LicensesDialog 2.2.0 pulls in androidx.webkit and uses setForceDark() instead of correctly
    // setting colors.
    //noinspection GradleDependency
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    // dev.chrisbanesinsetter:insetter:0.6.0 makes inset unstable when entering immersive.
    implementation 'dev.chrisbanes:insetter-ktx:0.3.1'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.shizuku:api:13.1.5'
    implementation ('eu.agno3.jcifs:jcifs-ng:2.1.10') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
    }
    implementation platform('io.coil-kt:coil-bom:2.7.0')
    implementation 'io.coil-kt:coil'
    implementation 'io.coil-kt:coil-gif'
    implementation 'io.coil-kt:coil-svg'
    implementation 'io.coil-kt:coil-video'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.5.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
    implementation 'me.zhanghai.android.foregroundcompat:library:1.0.2'
    implementation 'me.zhanghai.android.libarchive:library:1.1.3'
    implementation 'me.zhanghai.android.libselinux:library:2.1.0'
    implementation 'me.zhanghai.android.retrofile:library:1.2.0'
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'
    implementation 'org.apache.ftpserver:ftpserver-core:1.2.0'
    // This is a dependency of org.apache.ftpserver:ftpserver-core but org.apache.mina:mina-core
    // 2.1.3+ became incompatible before API 24 due to dependency on StandardSocketOptions
    // (DIRMINA-1123) and NetworkChannel.supportedOptions() (DIRMINA-1130).
    implementation ('org.apache.mina:mina-core') {
        version {
            strictly '2.1.3'
        }
    }
    // Also a dependency of jCIFS-NG.
    implementation 'org.slf4j:slf4j-android:1.7.36'

//#ifdef NONFREE
    implementation platform('com.google.firebase:firebase-bom:33.4.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
//#endif
}
